# Statup
Statup文件有啥用？
从名称来看，表示开始的意思。
1. 在系统上电或者复位时，先将CPU设置到正确的模式下面。一般CPU本身会有几种模式，比如对于ARM来说，这个时候一般会设置为管理模式。
1. 关闭所有的CPU中断。
2. 关闭内存管理单元MMU和TLB。
3. 关闭写缓冲和Cache。
4. 初始化内存控制器。
5. 设置CPU的PLL，设置时钟。
6. 创建堆栈。
7. 设置并打开MMU和Cache。
8. 如果需要，自己拷贝EBOOT从Flash到RAM中。
9. 跳转到C代码中，一般是Main函数或者EBootMain函数

## ARMCC编译器常见汇编指令
**符号**
一个符号表示当前位置所在的地址
```asm
__heap_base
Heap_Mem        SPACE   Heap_Size
__heap_limit

__Vectors       DCD     __initial_sp              ; Top of Stack
                DCD     Reset_Handler             ; Reset Handler
```
__heap_base：表示堆的基址
__heap_limit：表示申请完内存后的

**EQU**
例子：Stack   EQU     0x00002000
等价：#define Stack   0x00002000
**SPACE or FILL**
定义：
```asm
{label} SPACE expr 
{label} FILL expr{,value{,valuesize}} 
```
SPACE只申请内存
FILL申请内存后用value填充，并且一个value的大小由valuesize决定
例子：Stack_Mem  SPACE  Stack
解释：申请Stack个大小的RAM空间
**AREA**
定义：AREA sectionname{,attr}{,attr}... 
例子：AREA    STACK, NOINIT, READWRITE, ALIGN=3
例子：AREA    |1_Heap|, NOINIT, READWRITE, ALIGN=3
例子：AREA    |.text|, NOINIT, READWRITE, ALIGN=3
解释：定义段(Section)，段名为STACK，不初始化，可读可写，以3字节对齐；以非字母开头的都需要加|xxx|
**PRESERVE8**
例子：PRESERVE8
解释：表示当前汇编代码以8字节对齐
**EXPORT or GLOBAL**
定义：
```asm
EXPORT {[WEAK]}
EXPORT symbol {[SIZE=n]}
EXPORT symbol {[type{,set}]}
EXPORT symbol [attr{,type{,set}}{,SIZE=n}]
EXPORT symbol [WEAK {,attr}{,type{,set}}{,SIZE=n}]
```
例子：
```asm
        AREA    Example,CODE,READONLY
        EXPORT  DoAdd           ; Export the function name
                                ; to be used by external modules.
DoAdd   ADD     r0,r0,r1
```
解释：导出一个符号，可以被其他文件引用
解释：[WEAK]的意思是，被其他源文件引入的优先级较弱，如果有其他源代码导出了同名符号且不带[WEAK]，就优先引入其他源文件的符号。
**THUMB**
定义：THUMB
例子：THUMB
解释：表示后续的代码以THUMB(16位)机器码产生
**ARM**
定义：ARM
例子：ARM
解释：表示后续的代码以ARM(32位)机器码产生
**DCD**
定义：{label} DCD expr{,expr}
例子：DCD     0x1234
解释：以4字节为单位分配内存，要求4字节对齐，可以多个。

## ARMCC编译器Startup文件解释
```asm
; @file     startup_ARMCM0.s

;定义栈大小
Stack_Size      EQU     0x00000400
;定义Section：STACK，未初始化，可读可写，以3字节对齐
                AREA    STACK, NOINIT, READWRITE, ALIGN=3
;申请内存
Stack_Mem       SPACE   Stack_Size
__initial_sp    EQU     0x20008000     ;初始栈顶指针，栈方向是从[大地址]向[小地址]，用减法来压栈的。


;定义堆大小
Heap_Size       EQU     0x00000400
;定义Section：HEAP，未初始化，可读可写，以3字节对齐
                AREA    HEAP, NOINIT, READWRITE, ALIGN=3
__heap_base
Heap_Mem        SPACE   Heap_Size
__heap_limit

                PRESERVE8   ;下面以8字节对齐
                THUMB       ;下面代码翻译成Thumb指令集


;开始定义向量表，应该放在地址=0x00000000的位置

;定义Section：RESET，数据段，只读
                AREA    RESET, DATA, READONLY
                EXPORT  __Vectors            ;导出符号，给链接器，或者其他文件使用
                EXPORT  __Vectors_End        ;导出符号，给链接器，或者其他文件使用
                EXPORT  __Vectors_Size       ;导出符号，给链接器，或者其他文件使用

;开始定义向量表，向量表是一个数组，每个元素都是4字节，因为是32位架构，表示地址
;这些是内部的向量表
__Vectors       DCD     __initial_sp              ; Top of Stack
                DCD     Reset_Handler             ; Reset Handler
                DCD     NMI_Handler               ; NMI Handler
                DCD     HardFault_Handler         ; Hard Fault Handler
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     SVC_Handler               ; SVCall Handler
                DCD     0                         ; Reserved
                DCD     0                         ; Reserved
                DCD     PendSV_Handler            ; PendSV Handler
                DCD     SysTick_Handler           ; SysTick Handler

                ; External Interrupts  外部中断
                ; 每一个代表的是C语言函数的名称，刚好就是函数地址
                DCD     TMR0_IRQHandler           ;  0:  TMR0
                DCD     GPIO_IRQHandler           ;  1:  GPIO
                DCD     SLAVE_IRQHandler          ;  2:  SLAVE
                DCD     SPI0_IRQHandler           ;  3:  SPI0
                DCD     BB_IRQHandler             ;  4:  BB
                DCD     LLE_IRQHandler            ;  5:  LLE
                DCD     USB_IRQHandler            ;  6:  USB
                DCD     ETH_IRQHandler            ;  7:  ETH
                DCD     TMR1_IRQHandler           ;  8:  TMR1
                DCD     TMR2_IRQHandler           ;  9:  TMR2
                DCD     UART0_IRQHandler          ; 10:  UART0
                DCD     UART1_IRQHandler          ; 11:  UART1
                DCD     RTC_IRQHandler            ; 12:  RTC
                DCD     ADC_IRQHandler            ; 13:  ADC
                DCD     SPI1_IRQHandler           ; 14:  SPI1
                DCD     LED_IRQHandler            ; 15:  LED
                DCD     TMR3_IRQHandler           ; 16:  TMR3 
                DCD     UART2_IRQHandler          ; 17:  UART2
                DCD     UART3_IRQHandler          ; 18:  UART3
                DCD     WDT_IRQHandler            ; 19:  WDT
__Vectors_End
;计算出向量表大小
__Vectors_Size  EQU     __Vectors_End - __Vectors



;定义Section：.text，代码段，只读
                AREA    |.text|, CODE, READONLY

; Reset Handler
; 复位子程序是系统上电后第一个执行的程序
; 我们刚才定义了中断表，CPU会通过向量表进入Reset_Handler
Reset_Handler   PROC                             ;这是一个函数
                EXPORT  Reset_Handler   [WEAK]   ;导出这个函数
                IMPORT  SystemInit               ;导入SystemInit函数
                IMPORT  __main                   ;导入__main函数
				LDR     R0, =SystemInit          ;加载SystemInit地址到R0寄存器
                BLX     R0                       ;跳转到R0地址,把跳转前的下一条地址保存到LR,并根据寄存器的LSE确定处理器状态
                LDR     R0, =__main              ;加载__main地址到R0寄存器
                BX      R0                       ;跳转到R0地址执行，不用返回
                ENDP


; Dummy Exception Handlers (infinite loops which can be modified)

NMI_Handler     PROC
                EXPORT  NMI_Handler               [WEAK]
                B       .               ;无限循环
                ENDP
HardFault_Handler\                      ;\表示折行
                PROC
                EXPORT  HardFault_Handler         [WEAK]
;                B       .
                ENDP
SVC_Handler     PROC
                EXPORT  SVC_Handler               [WEAK]
                B       .
                ENDP
PendSV_Handler  PROC
                EXPORT  PendSV_Handler            [WEAK]
                B       .
                ENDP
SysTick_Handler PROC
                EXPORT  SysTick_Handler           [WEAK]
                B       .
                ENDP


;定义默认的Handler，如果执行了这段代码就会导出默认的Handler
;这一段全是WEAK导出，原因见WEAK的含义
;但是这个函数似乎一直没有被调用到，所以其实有没有这一段问题也不大
Default_Handler PROC
                EXPORT     TMR0_IRQHandler           [WEAK];  0:  TMR0
                EXPORT     GPIO_IRQHandler           [WEAK];  1:  GPIO
                EXPORT     SLAVE_IRQHandler          [WEAK];  2:  SLAVE
                EXPORT     SPI0_IRQHandler           [WEAK];  3:  SPI0
                EXPORT     BB_IRQHandler             [WEAK];  4:  BB
                EXPORT     LLE_IRQHandler            [WEAK];  5:  LLE
                EXPORT     USB_IRQHandler            [WEAK];  6:  USB
                EXPORT     ETH_IRQHandler            [WEAK];  7:  ETH
                EXPORT     TMR1_IRQHandler           [WEAK];  8:  TMR1
                EXPORT     TMR2_IRQHandler           [WEAK];  9:  TMR2
                EXPORT     UART0_IRQHandler          [WEAK]; 10:  UART0
                EXPORT     UART1_IRQHandler          [WEAK]; 11:  UART1
                EXPORT     RTC_IRQHandler            [WEAK]; 12:  RTC
                EXPORT     ADC_IRQHandler            [WEAK]; 13:  ADC
                EXPORT     SPI1_IRQHandler           [WEAK]; 14:  SPI1
                EXPORT     LED_IRQHandler            [WEAK]; 15:  LED
                EXPORT     TMR3_IRQHandler           [WEAK]; 16:  TMR3 
                EXPORT     UART2_IRQHandler          [WEAK]; 17:  UART2
                EXPORT     UART3_IRQHandler          [WEAK]; 18:  UART3
                EXPORT     WDT_IRQHandler            [WEAK]; 19:  WDT
                ;定义了默认中断处理的地址，这些符号是不占用地址的，都是B .这条语句的地址.
                TMR0_IRQHandler           ;  0:  TMR0
                GPIO_IRQHandler           ;  1:  GPIO
                SLAVE_IRQHandler          ;  2:  SLAVE
                SPI0_IRQHandler           ;  3:  SPI0
                BB_IRQHandler             ;  4:  BB
                LLE_IRQHandler            ;  5:  LLE
                USB_IRQHandler            ;  6:  USB
                ETH_IRQHandler            ;  7:  ETH
                TMR1_IRQHandler           ;  8:  TMR1
                TMR2_IRQHandler           ;  9:  TMR2
                UART0_IRQHandler          ; 10:  UART0
                UART1_IRQHandler          ; 11:  UART1
                RTC_IRQHandler            ; 12:  RTC
                ADC_IRQHandler            ; 13:  ADC
                SPI1_IRQHandler           ; 14:  SPI1
                LED_IRQHandler            ; 15:  LED
                TMR3_IRQHandler           ; 16:  TMR3 
                UART2_IRQHandler          ; 17:  UART2
                UART3_IRQHandler          ; 18:  UART3
                WDT_IRQHandler            ; 19:  WDT
                B       .                 ; 如果跳到Startup定义的默认中断函数，就会无限循环
                ENDP
                ALIGN                     ; 填充字节使地址对齐


; User Initial Stack & Heap

                IF      :DEF:__MICROLIB

                EXPORT  __initial_sp
                EXPORT  __heap_base
                EXPORT  __heap_limit

                ELSE

                IMPORT  __use_two_region_memory
                EXPORT  __user_initial_stackheap

__user_initial_stackheap PROC
                LDR     R0, =  Heap_Mem
                LDR     R1, =(Stack_Mem + Stack_Size)
                LDR     R2, = (Heap_Mem +  Heap_Size)
                LDR     R3, = Stack_Mem
                BX      LR
                ENDP

                ALIGN

                ENDIF


                END                     ; 提示编译器，已经到了汇编文件结尾
```

## ARM-GCC编译器Startup文件分析
### GNU汇编指令简述
```txt
.arm	汇编使用ARM指令
.thumb	汇编使用Thumb指令
.code16	汇编使用Thumb指令
.code32	汇编使用ARM指令
.long   4字节
.short  2字节
.byte   1字节
.comm  symbol, length：在bss段申请一段命名空间,该段空间的名称叫symbol, 长度为length. Ld连接器在连接会为它留出空间
.equ symbol, expression: 把某一个符号(symbol)定义成某一个值(expression).该指令并不分配空间
.macro 宏名 参数名列表: 定义一段宏代码，.macro表示代码的开始
.endm：表示宏代码的结束
.exitm：跳出宏
.equ：给符号赋值：.equ abc, 0x10，也可以abc = 0x10
.if
.ifdef
.ifndef
.else
.endif
.section section_name, "w"  @a,w,x
.align exp1,exp2：以exp1对齐，以exp2填充
```
下面是ARM-GCC Startup代码分析
```asm
/* @file     startup_ARMCM0.S   */

                .syntax  unified     /*.syntax [unified | divided]  ARM和THUMB使用统一的汇编格式*/
                .arch    armv6-m     /*指定ARM架构 为armv6-m*/

                .section .vectors    /*定义section*/
                .align   2           /*以2字节对齐*/
                .globl   __Vectors         /*导出符号*/
                .globl   __Vectors_End     /*导出符号*/
                .globl   __Vectors_Size    /*导出符号*/

/*开始定义向量表，分为CPU内部中断和外部中断，内部中断是负数，外部中断是从0开始的正数*/
__Vectors:
                .long    __StackTop                         /*     Top of Stack */
                .long    Reset_Handler                      /*     Reset Handler */
                .long    NMI_Handler                        /* -14 NMI Handler */
                .long    HardFault_Handler                  /* -13 Hard Fault Handler */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    SVC_Handler                        /*  -5 SVCall Handler */
                .long    0                                  /*     Reserved */
                .long    0                                  /*     Reserved */
                .long    PendSV_Handler                     /*  -2 PendSV Handler */
                .long    SysTick_Handler                    /*  -1 SysTick Handler */

                /* Interrupts */
                .long    Interrupt0_Handler                 /*   0 Interrupt 0 */
                .long    Interrupt1_Handler                 /*   1 Interrupt 1 */
                .long    Interrupt2_Handler                 /*   2 Interrupt 2 */
                .long    Interrupt3_Handler                 /*   3 Interrupt 3 */
                .long    Interrupt4_Handler                 /*   4 Interrupt 4 */
                .long    Interrupt5_Handler                 /*   5 Interrupt 5 */
                .long    Interrupt6_Handler                 /*   6 Interrupt 6 */
                .long    Interrupt7_Handler                 /*   7 Interrupt 7 */
                .long    Interrupt8_Handler                 /*   8 Interrupt 8 */
                .long    Interrupt9_Handler                 /*   9 Interrupt 9 */
                /*为10到31中断保留空间，但是没有地址*/
                .space   ( 22 * 4)                          /* Interrupts 10 .. 31 are left out */
__Vectors_End:
                .equ     __Vectors_Size, __Vectors_End - __Vectors
                .size    __Vectors, . - __Vectors

                
                .thumb               /*后面的代码使用thumb指令集*/
                .section .text       /*定义section*/
                .align   2           /*以2字节对齐*/

                .thumb_func           /*定义thumb指令集的函数*/
                .type    Reset_Handler, %function   /**/
                .globl   Reset_Handler
                .fnstart
Reset_Handler:
                bl       SystemInit

                ldr      r4, =__copy_table_start__
                ldr      r5, =__copy_table_end__



;无限循环函数，看到这里之后请直接跳过后面的 汇编代码。
.L_loop0:
                cmp      r4, r5
                bge      .L_loop0_done
                ldr      r1, [r4]                /* source address */
                ldr      r2, [r4, #4]            /* destination address */
                ldr      r3, [r4, #8]            /* word count */
                lsls     r3, r3, #2              /* byte count */

.L_loop0_0:
                subs     r3, #4                  /* decrement byte count */
                blt      .L_loop0_0_done
                ldr      r0, [r1, r3]
                str      r0, [r2, r3]
                b        .L_loop0_0

.L_loop0_0_done:
                adds     r4, #12
                b        .L_loop0

.L_loop0_done:

                ldr      r3, =__zero_table_start__
                ldr      r4, =__zero_table_end__

.L_loop2:
                cmp      r3, r4
                bge      .L_loop2_done
                ldr      r1, [r3]                /* destination address */
                ldr      r2, [r3, #4]            /* word count */
                lsls     r2, r2, #2              /* byte count */
                movs     r0, 0

.L_loop2_0:
                subs     r2, #4                  /* decrement byte count */
                blt      .L_loop2_0_done
                str      r0, [r1, r2]
                b        .L_loop2_0
.L_loop2_0_done:

                adds     r3, #8
                b        .L_loop2
.L_loop2_done:

                bl       _start

                .fnend
                .size    Reset_Handler, . - Reset_Handler

/* The default macro is not used for HardFault_Handler
 * because this results in a poor debug illusion.
 */
                .thumb_func
                .type    HardFault_Handler, %function
                .weak    HardFault_Handler
                .fnstart
HardFault_Handler:
                b        .
                .fnend
                .size    HardFault_Handler, . - HardFault_Handler

                .thumb_func
                .type    Default_Handler, %function
                .weak    Default_Handler
                .fnstart
Default_Handler:
                b        .
                .fnend
                .size    Default_Handler, . - Default_Handler

/* Macro to define default exception/interrupt handlers.
 * Default handler are weak symbols with an endless loop.
 * They can be overwritten by real handlers.
 */
                .macro   Set_Default_Handler  Handler_Name
                .weak    \Handler_Name
                .set     \Handler_Name, Default_Handler
                .endm


/* Default exception/interrupt handler */

                Set_Default_Handler  NMI_Handler
                Set_Default_Handler  SVC_Handler
                Set_Default_Handler  PendSV_Handler
                Set_Default_Handler  SysTick_Handler

                Set_Default_Handler  Interrupt0_Handler
                Set_Default_Handler  Interrupt1_Handler
                Set_Default_Handler  Interrupt2_Handler
                Set_Default_Handler  Interrupt3_Handler
                Set_Default_Handler  Interrupt4_Handler
                Set_Default_Handler  Interrupt5_Handler
                Set_Default_Handler  Interrupt6_Handler
                Set_Default_Handler  Interrupt7_Handler
                Set_Default_Handler  Interrupt8_Handler
                Set_Default_Handler  Interrupt9_Handler


                .end

```

从上述代码可见ARM的CMSIS为ARM-GCC所提供的Startup.S并不精简，单单一个Startup.S文件不足够。
还需要其对应的一个ld文件，这个ld文件又非常复杂，还需要一个startup.c文件，三个文件一起才能编译出Startup。
所以，在ARM-GCC上面，我们不用汇编来写Startup，直接用C语言来实现Startup。

## C语言写Startup
**startup.c**
```c
extern void Reset_Handler(void);
extern void NMI_Handler(void);
extern void HardFault_Handler(void);
extern void SVC_Handler(void);
extern void PendSV_Handler(void);
extern void SysTick_Handler(void);

extern void TMR0_IRQHandler(void);  // 0:  TMR0
extern void GPIO_IRQHandler(void);  // 1:  GPIO
extern void SLAVE_IRQHandler(void); // 2:  SLAVE
extern void SPI0_IRQHandler(void);  // 3:  SPI0
extern void BB_IRQHandler(void);    // 4:  BB
extern void LLE_IRQHandler(void);   // 5:  LLE
extern void USB_IRQHandler(void);   // 6:  USB
extern void ETH_IRQHandler(void);   // 7:  ETH
extern void TMR1_IRQHandler(void);  // 8:  TMR1
extern void TMR2_IRQHandler(void);  // 9:  TMR2
extern void UART0_IRQHandler(void); // 10:  UART0
extern void UART1_IRQHandler(void); // 11:  UART1
extern void RTC_IRQHandler(void);   // 12:  RTC
extern void ADC_IRQHandler(void);   // 13:  ADC
extern void SPI1_IRQHandler(void);  // 14:  SPI1
extern void LED_IRQHandler(void);   // 15:  LED
extern void TMR3_IRQHandler(void);  // 16:  TMR3
extern void UART2_IRQHandler(void); // 17:  UART2
extern void UART3_IRQHandler(void); // 18:  UART3
extern void WDT_IRQHandler(void);   // 19:  WDT
typedef void (*Function)(void);
extern unsigned int _Stack_Top;

__attribute__((section("vectors"))) Function VectorArray[] = {
    (Function)&_Stack_Top,
    Reset_Handler,
    NMI_Handler,
    HardFault_Handler,
    0,
    0,
    0,
    0,
    0,
    0,
    0,
    SVC_Handler,
    0,
    0,
    PendSV_Handler,
    SysTick_Handler,
    //外设中断
    TMR0_IRQHandler,  // 0:  TMR0
    GPIO_IRQHandler,  // 1:  GPIO
    SLAVE_IRQHandler, // 2:  SLAVE
    SPI0_IRQHandler,  // 3:  SPI0
    BB_IRQHandler,    // 4:  BB
    LLE_IRQHandler,   // 5:  LLE
    USB_IRQHandler,   // 6:  USB
    ETH_IRQHandler,   // 7:  ETH
    TMR1_IRQHandler,  // 8:  TMR1
    TMR2_IRQHandler,  // 9:  TMR2
    UART0_IRQHandler, // 10:  UART0
    UART1_IRQHandler, // 11:  UART1
    RTC_IRQHandler,   // 12:  RTC
    ADC_IRQHandler,   // 13:  ADC
    SPI1_IRQHandler,  // 14:  SPI1
    LED_IRQHandler,   // 15:  LED
    TMR3_IRQHandler,  // 16:  TMR3
    UART2_IRQHandler, // 17:  UART2
    UART3_IRQHandler, // 18:  UART3
    WDT_IRQHandler    // 19:  WDT
};
```
**startup_init.c**
```c
#include "CH57x_clk.h"
#include "CH57x_gpio.h"
#include "CH57x_uart.h"

extern int main(void);
extern unsigned int _lvmdata, _sdata, _edata, _sbss, _ebss;

void _copydata(unsigned int *form, unsigned int *section_begin, unsigned int *section_end)
{
  unsigned int *p = section_begin;
  while (p < section_end)
  {
    *(p++) = *(form++);
  }
}

void _bssinit(unsigned int *startbss, unsigned int *endbss)
{
  unsigned int *p = startbss;
  while (p < endbss)
  {
    *(p++) = 0;
  }
}

void Reset_Handler(void)
{
  _bssinit(&_sbss, &_ebss);
  _copydata(&_lvmdata, &_sdata, &_edata);
  SystemInit();
  main();
}
void NMI_Handler(void)
{
  while (1)
    ;
}
void HardFault_Handler(void)
{
  GPIOB_ModeCfg(GPIO_Pin_1, GPIO_ModeOut_PP_20mA);
  GPIOB_SetBits(GPIO_Pin_1);
  while (1)
    ;
}
void SVC_Handler(void)
{
  while (1)
    ;
}
void PendSV_Handler(void)
{
  while (1)
    ;
}
void SysTick_Handler(void)
{
}
```